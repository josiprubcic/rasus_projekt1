plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'idea'
    id 'eclipse'
}

apply plugin: 'java-library'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

group 'hr.fer.rassus'
version '1'


repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/"
    }
    mavenCentral()
    mavenLocal()
}


dependencies {
    //################ GRPC ################
    implementation 'io.grpc:grpc-netty-shaded:1.68.0'
    implementation 'io.grpc:grpc-protobuf:1.68.0'
    implementation 'io.grpc:grpc-services:1.68.0'
    implementation 'io.grpc:grpc-stub:1.68.0'

    //################ CSV ################
    implementation 'com.opencsv:opencsv:5.8'

    //################ REST - Retrofit ################
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    //################ JSON Processing ################
    implementation 'com.fasterxml.jackson.core:jackson-core:2.19.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'

    //################ Annotations ################
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    //################ Testing ################
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        // Remember to refresh gradle project in Eclipse (F5)
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

test {
    useJUnitPlatform()
}

//################ GRPC ################
// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.5"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.68.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        all().each {
            it.group = 'build'
        }
    }
}
//################ GRPC ################
task runExampleClient(type: JavaExec) {
    group = "exec"
    description = "Run client with JavaExecTask"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "hr.fer.tel.rassus.examples.SimpleUnaryRPCClient"
    standardInput = System.in

}

task runExampleServer(type: JavaExec) {
    group = "exec"
    description = "Run client with JavaExecTask"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "hr.fer.tel.rassus.examples.SimpleUnaryRPCServer"
}
